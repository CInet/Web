#!/usr/bin/env perl

# ABSTRACT: A simple HTTP server for static files with template support.

use Modern::Perl 2018;
use Mojolicious::Lite -signatures;

use Path::Tiny;
use File::LibMagic;
use Pod::Markdown;
use Pod::Simple::Search;

# Configuration and setup: use MOJO_CONFIG environment variable to set path.
my $config = plugin 'Config';

my $basedir = path($config->{basedir} // die "no 'basedir' path configured");
my $images  = path($config->{images}  // die "no 'images' path configured");
my $data    = path($config->{data}    // die "no 'data' path configured");

unshift app->renderer->paths->@*, $basedir->child('templates');
unshift app->static->paths->@*, my $site = $basedir->child('public');

sub directory_index {
    my $dir = path(shift);
    my @files = { name => '../', label => 'Parent directory', size => '', type => '', mtime => '' };
    for my $file (sort $dir->children) {
        push @files, {
            name  => $file->basename,
            label => $file->basename,
            size  => $file->stat->size,
            type  => (-d $file ? 'directory' : get_mimetype("$file")),
            mtime => Mojo::Date->new($file->stat->mtime)->to_string,
        };
    }
    @files
}

sub get_mimetype {
    my $file = shift;
    my $magic = File::LibMagic->new;
    open my $fh, '<', $file;
    my $info = $magic->info_from_handle($fh);
    close $fh;
    $info->{mime_type}
}

sub ensure_subsumes {
    my ($c, $dir, $path) = @_;
    # No upwards traversal
    if (not $dir->subsumes($path)) {
        $c->reply->not_found;
        return 0;
    }
    return 1;
}

sub ensure_directory_slash {
    my ($c, $path) = @_;
    # Always make sure that the browser has a trailing '/'
    # so that relative links work.
    if (-d $path and $c->req->url->path !~ m|/$|) {
        $c->req->url->path->trailing_slash(1);
        $c->redirect_to($c->req->url);
        return 0;
    }
    return 1;
}

sub handle_file {
    my ($c, $path) = @_;
    # If the file exists, serve it. If its mimetype is text/*,
    # then serve it as text.
    if (-f $path) {
        if (get_mimetype($path)) {
            $c->render(text => $path->slurp_utf8, format => 'txt');
        }
        else {
            $c->reply->file($path);
        }
        return 1;
    }
    return 0;
}

sub handle_markdown {
    my ($c, $path) = @_;
    $path = $path->child('index') if -d $path;
    $path = $path->sibling($path->basename . ".md");
    if (-f $path) {
        $c->stash(markdown => $path->slurp_utf8);
        $c->render('markdown');
        return 1;
    }
    return 0;
}

# Directory index of /images
get '/images/*anypath' => { anypath => undef } => sub ($c) {
    my $path = $images->child($c->stash('anypath') // '.');

    ensure_subsumes($c, $images => $path) or return;
    ensure_directory_slash($c, $path) or return;
    handle_file($c, $path) and return;

    if (-d $path) {
        $c->stash(files => [ directory_index($path) ]);
        $c->stash(reqpath => Mojo::Util::url_unescape($c->req->url->path));
        $c->render('directory');
        return;
    }

    $c->reply->not_found;
};

# Data repositories are all checked out by git post-receive hooks.
# We treat them as static files. These URLs also understand a ?dir
# GET parameter which will render a directory index instead of the
# index.md file.
get '/data/*anypath' => { anypath => undef } => sub ($c) {
    my $path = $data->child($c->stash('anypath') // '.');

    ensure_subsumes($c, $data => $path) or return;
    ensure_directory_slash($c, $path) or return;
    handle_file($c, $path) and return;

    if (-d $path and (defined $c->param('dir') or not -f $path->child('index.md'))) {
        $c->stash(files => [ directory_index($path) ]);
        $c->stash(reqpath => Mojo::Util::url_unescape($c->req->url->path));
        $c->render('directory');
        return;
    }

    handle_markdown($c, $path) and return;

    $c->reply->not_found;
};

# Serve documentation of installed Perl modules
get '/doc/:module' => sub ($c) {
    my $search = Pod::Simple::Search->new;
    my $parser = Pod::Markdown->new(
        perldoc_url_prefix => '/doc/'
    );

    my $path = $search->find($c->stash('module'));
    if (not $path) {
        $c->reply->not_found;
        return;
    }

    $parser->output_string(\my $markdown);
    $parser->parse_file($path);

    $c->stash(markdown => $markdown);
    $c->render('markdown');
};

# Catch all: serve static files or render markdown files.
get '/*anypath' => { anypath => '' } => sub ($c) {
    my $path = $site->child(
        $c->stash('anypath') || 'index'
    );

    ensure_subsumes($c, $site => $path) or return;
    ensure_directory_slash($c, $path) or return;
    handle_file($c, $path) and return;
    handle_markdown($c, $path) and return;

    $c->reply->not_found;
};

app->start;
